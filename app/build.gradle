apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'kotlin'

apply plugin: 'application'
mainClassName = 'se.lovef.linedrawer.Main'

sourceCompatibility = 1.5

dependencies {
    compile project(":data")
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'no.tornado:tornadofx:1.6.+'

    testCompile 'se.lovef.test:test:1.0.8'
}

apply plugin: 'javafx-gradle-plugin'

//noinspection GroovyAssignabilityCheck
run {
    if (project.hasProperty("runargs")) {
        def arguments = project["runargs"]
        println("Args: " + arguments)
        //noinspection GroovyAssignabilityCheck
        args arguments.split(',')
    }
}

//noinspection GroovyAssignabilityCheck
task screenSaver(dependsOn: 'classes', type: JavaExec, group: 'application', description: 'Run screen saver') {
    classpath = sourceSets.main.runtimeClasspath
    main mainClassName
    //noinspection GroovyAssignabilityCheck
    args '/s'
}

archivesBaseName = "Line-drawer"

jfx {
    appName = archivesBaseName
    mainClass = mainClassName

    // minimal requirement for jfxNative-task
    vendor = 'lovef'
}

//noinspection GroovyAssignabilityCheck
task buildScreenSaver(type: Copy, dependsOn: 'jfxNative') {
    from "build/jfx/native/${archivesBaseName}/"
    into "build/jfx/native/${archivesBaseName}/"
    include "${archivesBaseName}.exe"
    rename "${archivesBaseName}.exe", "${archivesBaseName}.scr"
}
